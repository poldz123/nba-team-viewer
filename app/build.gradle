apply plugin: 'com.android.application'
apply plugin: 'com.adarshr.test-logger'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: '../plugins/jacoco-plugin.gradle'
apply from: '../plugins/detekt-plugin.gradle'

android {
    compileSdkVersion rootProject.ext.applicationCompileSdkVersion
    buildToolsVersion rootProject.ext.buildToolVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.applicationMinSdkVersion
        targetSdkVersion rootProject.ext.applicationTargetSdkVersion
        versionCode rootProject.ext.applicationVersionCode
        versionName rootProject.ext.applicationVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        test {
            resources.srcDir "src/test/resources"
        }
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true
        unitTests.includeAndroidResources = true
        unitTests.all {
            // Apply the gradle logs which the custom logger will configure
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        // TODO: Removed when activity and fragment ktx rc versions is not needed anymore
        // https://stackoverflow.com/questions/48988778/cannot-inline-bytecode-built-with-jvm-target-1-8-into-bytecode-that-is-being-bui
        jvmTarget = "1.8"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

// Task used to do code formatting to the entire source code.
task detektFormat(type: io.gitlab.arturbosch.detekt.Detekt) {
    group = "Code Format"
    description = "Runs a detekt with autocorrect code formatting."
    source = files("src/main/java")
    config.from(files("$rootDir/plugins/data/detekt-format-plugin-config.yml"))
    autoCorrect = true
    ignoreFailures = true
    reports {
        xml.enabled = false
        html.enabled = false
    }
    include("**/*.kt")
    include("**/*.kts")
    exclude("resources/")
    exclude("build/")
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // AndroidX Google Library
    implementation "androidx.core:core-ktx:$googleCoreKtxVersion"
    implementation "androidx.appcompat:appcompat:$googleAppcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$googleConstraintLayoutVersion"
    implementation "androidx.activity:activity-ktx:$googleActivityKtxVersion"
    implementation "androidx.recyclerview:recyclerview:$googleRecyclerViewVersion"

    // Community libraries
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitLibraryVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinLibraryVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaLibraryVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidLibraryVersion"
    implementation "com.squareup.moshi:moshi:$moshiLibraryVersion"
    implementation "com.google.dagger:dagger:$daggerLibraryVersion"

    // Community libraries annotation processors
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiLibraryVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerLibraryVersion"
    kaptTest "com.google.dagger:dagger-compiler:$daggerLibraryVersion"

    // Test Library
    testImplementation "junit:junit:$junitLibraryVersion"
    testImplementation "org.robolectric:robolectric:$robolectricLibraryVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpMockWebserverLibraryVersion"
    testImplementation "com.google.truth:truth:$googleTrustLibraryVersion"
    testImplementation "androidx.test.ext:junit:$googleTestSupport"
    testImplementation "androidx.test:rules:$googleTestSupport"
}